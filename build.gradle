buildscript {
  repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://dl.bintray.com/jspcore/maven' }
  }

  dependencies {
  }
}

plugins {
  id "com.jfrog.bintray" version "1.7.3"
}

group 'com.jspcore'
version '0.4.6-SNAPSHOT'
description = 'A testing library for assert logging messages.'

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  jcenter()
}

ext {
  junit5_version = "5.2.0"
}
dependencies {
  compile "org.slf4j:slf4j-api:1.7.22"
  compile "ch.qos.logback:logback-classic:1.1.8"
  compile "org.hamcrest:hamcrest-library:1.3"
  compile "com.jayway.awaitility:awaitility:1.7.0"
  compile "junit:junit:4.12"
  compile "org.junit.jupiter:junit-jupiter-api:$junit5_version"
  compile 'org.spockframework:spock-core:1.1-groovy-2.4'
  
  testCompile "org.assertj:assertj-core:3.6.1"
  testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit5_version"
  testCompile "org.junit.jupiter:junit-jupiter-params:$junit5_version"
  testRuntime "org.junit.vintage:junit-vintage-engine:$junit5_version"
}

test {
  useJUnitPlatform {
    includeEngines 'junit-jupiter', 'junit-vintage'
  }
  testLogging {
    events "passed", "skipped", "failed"
  }
}

apply plugin: "jacoco"
jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

def pomConfig = {
  name project.name
  description project.description
  url "http://www.jspcore.com/"

  licenses {
    license {
      name "MIT License"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
  scm {
    connection "scm:https://mustaine@github.com/mustaine/logcapture.git"
    developerConnection "scm:git@github.com:mustaine/logcapture.git"
    url "https://github.com/mustaine/logcapture"
  }
  developers {
    developer {
      id "jspcore"
      name "Team JSPCore"
    }
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.8.1'
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      groupId 'com.jspcore'
      artifactId 'logcapture'
      version '0.4.6'

      artifacts {
        artifact sourceJar {
          classifier "sources"
        }

        artifact javadocJar {
          classifier "javadoc"
        }
      }

      pom.withXml {
        def root = asNode()
        asNode().appendNode('description', project.description)
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
//  dryRun = true
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

  publications = ['MyPublication']
  pkg {
    repo = 'maven'
    name = 'logcapture'
    userOrg = 'jspcore'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/mustaine/logcapture.git'
    version {
      name = '0.4.6'
      vcsTag = '0.4.6'
      released = new Date()
    }
  }
}



