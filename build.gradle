group 'org.logcapture'
version '1.2.2'
description = 'A testing library for assert logging messages.'

repositories {
  mavenCentral()
}

subprojects {
  apply plugin: 'idea'
  apply plugin: 'signing'
  apply plugin: 'maven-publish'
  apply plugin: 'java-library'
  apply plugin: "jacoco"

  repositories {
    mavenCentral()
  }

  test {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  java {
    withJavadocJar()
    withSourcesJar()
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      html.enabled true
    }
  }

  def pomConfig = {
    name project.name
    description 'A testing library for assert logging messages.'
    url "http://www.jspcore.com/"

    licenses {
      license {
        name "MIT License"
        url "https://opensource.org/licenses/MIT"
        distribution "repo"
      }
    }
    scm {
      connection "scm:https://jsalinaspolo@github.com/jsalinaspolo/logcapture.git"
      developerConnection "scm:git@github.com:jsalinaspolo/logcapture.git"
      url "https://github.com/jsalinaspolo/logcapture"
    }
    developers {
      developer {
        id "jsalinaspolo"
        name "Team JSPCore"
      }
    }
  }

  publishing {
    repositories {
      maven {
        name = "OSSRH"
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        credentials {
          username = System.getenv("MAVEN_USERNAME")
          password = System.getenv("MAVEN_PASSWORD")
        }
      }
    }
    publications {
      MyPublication(MavenPublication) {
        from components.java
        groupId 'org.logcapture'
        artifactId project.name
        version "1.2.2"

        pom.withXml {
          def root = asNode()
          root.appendNode('description', 'A testing library for assert logging messages.')
          root.children().last() + pomConfig
        }
      }
    }
  }

  signing {
    sign publishing.publications.MyPublication
  }

}

